// Closed Predicates
*Sub(relation, relation)
*RSub(relation, relation)
*Mut(relation, relation)
*RMut(relation, relation)
*Inv(relation, relation)
*Domain(relation, category)
*Range2(relation, category)
*SameEntity(entity, entity)
*ValCat(entity, category)
*ValRel(entity, entity, relation)
*CandCat_General(entity, category)
*CandRel_General(entity, entity, relation)
*CandCat_CBL(entity, category)
*CandRel_CBL(entity, entity, relation)
*CandCat_CMC(entity, category)
*CandCat_CPL(entity, category)
*CandRel_CPL(entity, entity, relation)
*CandCat_Morph(entity, category)
*CandCat_SEAL(entity, category)
*CandRel_SEAL(entity, entity, relation)
*PromCat_General(entity, category)
*PromRel_General(entity, entity, relation)

// Target Predicates
Cat(entity, category)
Rel(entity, entity, relation)

// Scoping (blocking) Rules
Cat(a, b) := ValCat(a, b) .
Rel(a, b, c) := ValRel(a, b, c) .

// Rules
// All weights have been scalled to accomodate prior (max was 7500).
0.00333 SameEntity(a, b), Cat(a, c) => Cat(b, c)
0.00333 SameEntity(a, b), Rel(a, z, r) => Rel(b, z, r)
0.00333 SameEntity(a, b), Rel(z, a, r) => Rel(z, b, r)
0.01333 Sub(c, d), Cat(a, c) => Cat(a, d)
0.01333 RSub(r, s), Rel(a, b, r) => Rel(a, b, s)
0.01333 Mut(c, d), Cat(a, c) => !Cat(a, d)
0.01333 RMut(r, s), Rel(a, b, r) => !Rel(a, b, s)
0.01333 Inv(r, s), Rel(a, b, r) => Rel(b, a, s)
0.01333 Domain(r, c), Rel(a, b, r) => Cat(a, c)
0.01333 Range2(r, c), Rel(a, b, r) => Cat(b, c)
0.00013 CandCat_General(a, c) => Cat(a, c)
0.00013 CandRel_General(a, b, r) => Rel(a, b, r)
0.00013 CandCat_CBL(a, c) => Cat(a, c)
0.00013 CandRel_CBL(a, b, r) => Rel(a, b, r)
0.00013 CandCat_CMC(a, c) => Cat(a, c)
0.00013 CandCat_CPL(a, c) => Cat(a, c)
0.00013 CandRel_CPL(a, b, r) => Rel(a, b, r)
0.00013 CandCat_Morph(a, c) => Cat(a, c)
0.00013 CandCat_SEAL(a, c) => Cat(a, c)
0.00013 CandRel_SEAL(a, b, r) => Rel(a, b, r)
0.00013 PromCat_General(a, c) => Cat(a, c)
0.00013 PromRel_General(a, b, r) => Rel(a, b, r)

// Priors encoded in data.
