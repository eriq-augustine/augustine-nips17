// Closed Predicates
*Sub(relation, relation)
*RSub(relation, relation)
*Mut(relation, relation)
*RMut(relation, relation)
*Inv(relation, relation)
*Domain(relation, category)
*Range2(relation, category)
*SameEntity(entity, entity)
*ValCat(entity, category)
*ValRel(entity, entity, relation)
*PSeedCat(entity, category)
*PSeedRel(entity, entity, relation)
*NSeedCat(entity, category)
*NSeedRel(entity, entity, relation)
*TrCat(entity, category)
*TrRel(entity, entity, relation)
*CandCat(entity, category)
*CandRel(entity, entity, relation)
*CandCat_General(entity, category)
*CandRel_General(entity, entity, relation)
*CandCat_CBL(entity, category)
*CandRel_CBL(entity, entity, relation)
*CandCat_CMC(entity, category)
*CandRel_CMC(entity, entity, relation)
*CandCat_CPL(entity, category)
*CandRel_CPL(entity, entity, relation)
*CandCat_Morph(entity, category)
*CandRel_Morph(entity, entity, relation)
*CandCat_SEAL(entity, category)
*CandRel_SEAL(entity, entity, relation)
*PromCat(entity, category)
*PromRel(entity, entity, relation)
*PromCat_General(entity, category)
*PromRel_General(entity, entity, relation)
*PattCat(entity, category)
*PattRel(entity, entity, relation)

// Target Predicates
Cat(entity, category)
Rel(entity, entity, relation)

// Rules
// All weights have been scalled to accomodate prior (max was 7500).
0.00333 ValCat(b, c), SameEntity(a, b), Cat(a, c) => Cat(b, c)
0.00333 ValRel(b, z, r), SameEntity(a, b), Rel(a, z, r) => Rel(b, z, r)
0.00333 ValRel(z, b, r), SameEntity(a, b), Rel(z, a, r) => Rel(z, b, r)
0.01333 ValCat(a, d), Sub(c, d), Cat(a, c) => Cat(a, d)
0.01333 ValRel(a, b, s), RSub(r, s), Rel(a, b, r) => Rel(a, b, s)
0.01333 ValCat(a, d), Mut(c, d), Cat(a, c) => !Cat(a, d)
0.01333 ValRel(a, b, s), RMut(r, s), Rel(a, b, r) => !Rel(a, b, s)
0.01333 ValRel(b, a, s), Inv(r, s), Rel(a, b, r) => Rel(b, a, s)
0.01333 ValCat(a, c), Domain(r, c), Rel(a, b, r) => Cat(a, c)
0.01333 ValCat(b, c), Range2(r, c), Rel(a, b, r) => Cat(b, c)
1.00000 ValCat(a, c), PSeedCat(a, c) => Cat(a, c)
1.00000 ValRel(a, b, r), PSeedRel(a, b, r) => Rel(a, b, r)
1.00000 ValCat(a, c), NSeedCat(a, c) => !Cat(a, c)
1.00000 ValRel(a, b, r), NSeedRel(a, b, r) => !Rel(a, b, r)
0.00013 ValCat(a, c), CandCat(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_General(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_General(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_CBL(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_CBL(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_CMC(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_CMC(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_CPL(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_CPL(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_Morph(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_Morph(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), CandCat_SEAL(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), CandRel_SEAL(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), PattCat(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), PattRel(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), PromCat(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), PromRel(a, b, r) => Rel(a, b, r)
0.00013 ValCat(a, c), PromCat_General(a, c) => Cat(a, c)
0.00013 ValRel(a, b, r), PromRel_General(a, b, r) => Rel(a, b, r)

// Priors encoded in data.
